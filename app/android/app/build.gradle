def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 28
    // TODO(JonasWanke): https://github.com/flutter/flutter/issues/53657
    ndkVersion '21.3.6528147'

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "org.schulcloud.android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        release {
            storeFile rootProject.file('upload.keystore.jks')
            storePassword System.getenv("UPLOAD_KEY_STORE_PASSWORD")
            keyAlias "schulcloud"
            keyPassword System.getenv("UPLOAD_KEY_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            if (System.getenv("RELEASE_USE_DEBUG_KEY") == "true") {
                signingConfig signingConfigs.debug
            } else {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "brand"
    productFlavors {
        sc {
            dimension "brand"
        }
        sc_test {
            dimension "brand"
            applicationId "org.schulcloud.test.android"
        }
        brb {
            dimension "brand"
            applicationId "org.schulcloud.brandenburg.android"
        }
        n21 {
            dimension "brand"
            applicationId "cloud.niedersachsen.android"
        }
        nbc_audit {
            dimension "brand"
            applicationId "cloud.niedersachsen.audit.android"
        }
        open {
            dimension "brand"
            applicationId "org.schulcloud.open.android"
        }
        thr {
            dimension "brand"
            applicationId "de.schulcloudthueringen.android"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation("androidx.localbroadcastmanager:localbroadcastmanager:1.0.0")
}
